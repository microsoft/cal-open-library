OBJECT Codeunit 132592 XmlStream Utils Test
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Modified=Yes;
    Version List=;
  }
  PROPERTIES
  {
    Subtype=Test;
    TestPermissions=Disabled;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Assert@1000 : Codeunit 130000;


    [Test]
    LOCAL PROCEDURE TestXmlDecompression@1();
    VAR
      TempBlob@1008 : TEMPORARY Record 99008535;
      MtomString@1007 : Text;
      LineBreak@1006 : Text;
      XmlDocumentResponse@1005 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      MtomInStream@1004 : InStream;
      XmlInStream@1003 : InStream;
      MtomOutStream@1002 : OutStream;
      XmlStreamUtils@1001 : Codeunit 705;
      Result@1009 : Text;
      Content@1010 : Text;
    BEGIN
      LineBreak[1] := 13;
      LineBreak[2] := 10;

      Content    := '<env:Envelope xmlns:env="http://www.w3.org/2003/05/soap-envelope"><env:Header /><env:Body>';
      Content    += '<ns2:test xmlns:ns2="https://ns2" />';
      Content    += '</env:Body></env:Envelope>';

      MtomString := '------=_Part_170043_1574782596.1513784534431' + LineBreak;
      MtomString += 'Content-Type: application/xop+xml; charset=utf-8; type="application/soap+xml"' + LineBreak;
      MtomString += '' + LineBreak;
      MtomString += Content + LineBreak;
      MtomString += '------=_Part_170043_1574782596.1513784534431--';

      TempBlob.Blob.CREATEOUTSTREAM(MtomOutStream);
      TempBlob.Blob.CREATEINSTREAM(MtomInStream);
      MtomOutStream.WRITETEXT(MtomString);

      XmlStreamUtils.MtomStreamToXmlStream(MtomInStream,XmlInStream,'Multipart/Related; boundary="----=_Part_170043_1574782596.1513784534431"; type="application/xop+xml"; start-info="application/soap+xml"');

      XmlDocumentResponse := XmlDocumentResponse.XmlDocument();
      XmlDocumentResponse.PreserveWhitespace := TRUE;
      XmlDocumentResponse.Load(XmlInStream);
      Result := XmlDocumentResponse.InnerXml;

      // verify the result
      Assert.AreEqual(Content, Result, 'XmlStreamUtils.MtomStreamToXmlStream returned the wrong value');
    END;

    BEGIN
    END.
  }
}

